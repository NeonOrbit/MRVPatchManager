//file:noinspection DependencyNotationArgument
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
apply plugin: "kotlin-kapt"
apply from: "dependency.gradle"

android {
    namespace 'app.neonorbit.mrvpatchmanager'
    compileSdk 35

    defaultConfig {
        minSdk 28
        targetSdk 35
        versionCode 2340
        versionName "2.3.4"
        applicationId namespace
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = '21'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    packagingOptions {
        resources {
            excludes += [
                    'XPP3_*_VERSION',
                    'DebugProbesKt.bin',
                    'META-INF/CHANGES',
                    'META-INF/README.md'
            ]
        }
    }

    applicationVariants.configureEach { variant ->
        def buildType = variant.name.capitalize()
        def apkTaskName = "copy${buildType}Apk"
        def nameSuffix = variant.name == "release" ? '' : "-${variant.name}"
        def outFileName = "${project.name}-v${variant.versionName}${nameSuffix}.apk"
        variant.outputs.configureEach {
            outputFileName = outFileName
        }
        tasks.register("$apkTaskName", Copy) {
            from file("$rootProject.projectDir/app/$variant.name/$outFileName")
            into file("$rootProject.projectDir/out")
            doLast {
                delete file("$rootProject.projectDir/app/$variant.name/$outFileName")
            }
        }
    }

    afterEvaluate {
        assembleDebug.finalizedBy(copyDebugApk)
        assembleRelease.finalizedBy(copyReleaseApk)
    }
}

dependencies {
    implementation mrvpatcher('5.7.4', '21be4a0aa0a28c7a4bc87b1d037f1dfaa9d63a37')

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'

    implementation 'androidx.navigation:navigation-ui-ktx:2.8.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.3'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'com.google.android.material:material:1.12.0'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:converter-simplexml:2.11.0'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.2'

    // https://mvnrepository.com/artifact/org.greenrobot/eventbus
    implementation 'org.greenrobot:eventbus:3.3.1'

    // https://mvnrepository.com/artifact/com.github.bumptech.glide/glide
    //noinspection KaptUsageInsteadOfKsp
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation('com.github.bumptech.glide:recyclerview-integration:4.16.0') {
        transitive false
    }

    // https://mvnrepository.com/artifact/dev.rikka.rikkax.material/material-preference
    implementation ("dev.rikka.rikkax.material:material-preference:2.0.0") {
        exclude group: "dev.rikka.rikkax.appcompat", module: "appcompat"
        exclude group: "androidx.lifecycle", module: "lifecycle-viewmodel-ktx"
    }

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
